plugins {
    // AsciiDoc 파일을 컨버팅하고 Build 폴더에 복사하기 위한 플러그인입니다.
    id "org.asciidoctor.convert" version "1.5.10"
}

dependencies {
    compile project(':webtoon-core')
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-test'
    implementation ('org.springframework.boot:spring-boot-starter-actuator')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

ext {
    snippetsDir = file('build/generated-snippets')          // (2)
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    // gradle build 시 test -> asciidoctor 순으로 수행됩니다.
    dependsOn test
}


bootJar {
    // gradle build 시 asciidoctor -> bootJar 순으로 수행됩니다.
    dependsOn asciidoctor

    // gradle build 시 ./build/asciidoc/html5/ 에 html 파일이 생깁니다.
    // 이것을 jar 안에 /static/docs/ 폴더에 복사가 됩니다.
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}